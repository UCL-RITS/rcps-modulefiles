#%Module
#
# Matlab R2013a
#
## Added 29/June/2012 by OK
## Modified for R2012b and setup of .matlab directory 21st Sept 2012 by BAA
## Modified for R2013a (Matlab: 8.1.0.604) 26th March 2013 by BAA
## Modified with fixes for Matlab runtime and GCC 26th April 2013 by BAA
## Modified to include support for the NAG Toolbox for Matlab March 2014 by BAA
## Updated for Upgraded Legion December 2015

proc ModulesHelp { } {
    puts stderr "Adds Matlab R2013a to your environment."
    puts stderr ""
    puts stderr "This is the full Matlab, not just the runtime environment and"
    puts stderr "has all the toolboxes included in UCL's Total Academic"
    puts stderr "Headcount-Campus licence.  You can submit single node"
    puts stderr "multi-threaded Matlab jobs and multi-node Matlab jobs that can"
    puts stderr "fully utilise the Parallel Computing Toolbox and Matlab's"
    puts stderr "Distributed Computing Server."
    puts stderr ""
    puts stderr "The NAG Toolbox for Matlab is also included:"
    puts stderr ""
    puts stderr "    http://www.nag.co.uk/numeric/mb/design.asp"
    puts stderr ""
    puts stderr "Directory: /shared/ucl/apps/Matlab/R2013a/full"
}

proc setupLink { from to } {

    if { [catch {file link $from $to} err] } {
	puts stderr "Cannot link from $from to $to:"
	puts stderr "    $err"
	return false
    }
    return true
}

proc appDirSetup { appDir } {

    set target "~/Scratch/[file tail $appDir]"

    if { [catch {file type $appDir} type] } {	# appDir does not exist yet
    	puts stderr ""
    	puts stderr "$appDir doesn't exist - creating as symbolic link to Scratch"
	puts stderr ""
	catch {file mkdir $target} err
	return [setupLink $appDir $target]
    }
    
    if { [string compare $type "link"] == 0 } { # is a symbolic link
    	puts stderr ""
    	puts stderr "$appDir is a symbolic link pointing to [file readlink $appDir]"
	puts stderr ""
	puts stderr "Note: this MUST be in your Scratch area."
	puts stderr ""
	return true
    }
    
    if { [string compare $type "directory"] == 0 } { # need to move existing dir to Scratch
    	puts stderr ""
	puts stderr "Moving existing $appDir to Scratch"
	puts stderr ""
	if { [file exists $target] } { # don't trash user's exsiting files
	    puts stderr "$target already exists - will not be overwritten."
	    return false
	}
	if { [catch {file rename $appDir $target}] } {
	    puts stderr "Cannot move $appDir to $target:"
	    puts stderr "    $err"
	    return false
	}
	puts stderr "creating symbolic link from $appDir to $target"
	puts stderr ""
	return [setupLink $appDir $target]
    }

    return false    
}


module-whatis "Adds Matlab R2013a to your environment."

conflict matlab
# prereq gcc-libs/4.9.2

# matlab loads an old libtiff that breaks gdal
conflict gdal

# Location of Matlab and NAG Toolbox
set prefix /shared/ucl/apps/Matlab/R2013a/full
set nagtoolbox /shared/ucl/apps/NAG/matlab-toolbox/MBL6A24DNL_R2015a

# Location of Matlab prefs and settings directory
set matlabDir "~/.matlab"

append-path PATH                $prefix/bin
append-path LD_LIBRARY_PATH	$prefix/runtime/glnxa64:$prefix/bin/glnxa64:$nagtoolbox/mex.a64
setenv MCRPATH                  $prefix

# Set environment variable for the NAG licence manager if not set

if { ! [info exists ::env(NAG_KUSARI_FILE)]} {
    setenv  NAG_KUSARI_FILE	lm-server:,naglm-server.ucl.ac.uk:
}

# Setup .matlab in Scratch

if { ! [appDirSetup $matlabDir] } {
    puts stderr ""
    puts stderr "**** error: setting up .matlab directory failed."
    puts stderr ""
    puts stderr "Note: you must load the Matlab module at least once from a login node."
    puts stderr ""
}

